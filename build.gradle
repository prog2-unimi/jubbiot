plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.diffplug.spotless' version '6.25.0'
}

group = 'it.unimi.di.prog2'
version = '0.4a0'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter:5.11.1'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  api 'org.junit.jupiter:junit-jupiter:5.11.1'
}

java {
  withJavadocJar()
  withSourcesJar()
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      name "GitLabDIPackages"
      url "https://gitlab.di.unimi.it/api/v4/projects/7715/packages/maven"
      credentials(HttpHeaderCredentials) {
        name = 'Private-Token'
        value = System.getenv('GITLAB_TOKEN')
      }
      authentication {
        header(HttpHeaderAuthentication)
      }
    }
  }
}

spotless {
  enforceCheck false
  java {
    googleJavaFormat('1.23.0')
    removeUnusedImports()
    indentWithSpaces(2)
    endWithNewline()
    licenseHeaderFile 'src/licenseHeaderFile.txt'
  }
}

tasks.named('test') {
  useJUnitPlatform()
  testLogging {
    events 'skipped', 'failed'
  }
}
