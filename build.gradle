plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.diffplug.spotless' version '8.0.0'
}

group = 'it.unimi.di.prog2'
version = '0.5a0'

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter:6.0.0'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  api 'org.junit.jupiter:junit-jupiter:6.0.0'
}


java {
  withJavadocJar()
  withSourcesJar()
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      name = "GitLabDIPackages"
      url = "https://gitlab.di.unimi.it/api/v4/projects/7715/packages/maven"
      credentials(HttpHeaderCredentials) {
        name = 'Private-Token'
        value = System.getenv('GITLAB_TOKEN')
      }
      authentication {
        header(HttpHeaderAuthentication)
      }
    }
  }
}


tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
  options.compilerArgs += ['-Xlint:all', '-Werror']
}

tasks.withType(JavaExec).configureEach {
  defaultCharacterEncoding = 'UTF-8'
}

tasks.withType(Test).configureEach {
  defaultCharacterEncoding = 'UTF-8'
}


spotless {
  enforceCheck = false
  java {
    googleJavaFormat('1.28.0')
    removeUnusedImports()
    leadingTabsToSpaces(2)
    endWithNewline()
    licenseHeaderFile('src/licenseHeaderFile.txt')
  }
}


tasks.named('test') {
  useJUnitPlatform()
  testLogging {
    events 'skipped', 'failed'
  }
}
