plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.diffplug.spotless' version '6.22.0'
}

group = 'it.unimi.di.prog2'
version = '0.2a0'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  api 'org.junit.jupiter:junit-jupiter:5.9.3'
}

java {
  withJavadocJar()
  withSourcesJar()
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/prog2-unimi/jubbiot"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}

spotless {
  enforceCheck false
  java {
    googleJavaFormat('1.17.0')
    removeUnusedImports()
    indentWithSpaces(2)
    endWithNewline()
    licenseHeaderFile 'src/licenseHeaderFile.txt'
  }
}

tasks.named('test') {
  useJUnitPlatform()
  testLogging {
    events 'skipped', 'failed'
  }
}
